/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives sourceJar
    archives groovydocJar
}

publishing {
    publications {
        deployment(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact groovydocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null
    publications = ['deployment']
    pkg {
        repo = 'maven'
        name = 'kobo-commons'
        desc = 'A helper library for Groovy, containing a supplement of standard API, a proposal of new features and so on.'
        websiteUrl = 'https://github.com/kobo/kobo-commons'
        issueTrackerUrl = 'https://github.com/kobo/kobo-commons/issues'
        vcsUrl = 'https://github.com/kobo/kobo-commons.git'
        licenses = ['Apache-2.0']
        labels = ['groovy']
        version {
            name = "${project.version}"
            vcsTag = "v${project.version}"
        }
    }
    dryRun = Boolean.valueOf(System.getProperty("bintray.dryRun") ?: false)
}

bintrayUpload.dependsOn build
